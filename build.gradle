buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

apply plugin: 'application'


group = 'org.satran.aion'
version = '0.3.0-SNAPSHOT'

description = "aion-graphql"

sourceCompatibility = 1.9

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {

    mavenCentral()
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools'

    compile group: 'com.graphql-java', name: 'graphql-java-tools', version:'5.2.4'
    compile group: 'com.graphql-java', name: 'graphql-spring-boot-starter', version:'5.0.2'
    compile group: 'com.graphql-java', name: 'graphiql-spring-boot-starter', version:'4.0.0'

    compile group: 'org.apache.commons', name: 'commons-pool2', version:'2.2'
    compile group: 'commons-beanutils', name: 'commons-beanutils', version:'1.9.3'

    compile "io.reactivex.rxjava2:rxjava:2.1.5"

    compile fileTree(include: ['*.jar'], dir: 'lib')
    compile fileTree(include: ['*.jar'], dir: 'lib/lib')
}

mainClassName = 'org.satran.blockchain.graphql.ChainGraphQLApplication'

jar {
    enabled = true
}

[bootJar, bootDistTar, bootDistZip]*.enabled = false

distZip {
    eachFile { it.duplicatesStrategy = 'exclude'};
}

distributions {
    main {
        baseName = 'aion-graphql'

        contents {

            from('native') {
                into('native')
            }

            from('src/main/resources') {
                into('config')
                include 'application.yml'
                include 'logback.xml'
            }
        }
    }
}

release {
    failOnUnversionedFiles = false
    failOnUpdateNeeded = false
    failOnCommitNeeded = false

    scmAdapters = [
            net.researchgate.release.GitAdapter,
    ]

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
        pushToBranchPrefix = ''
        commitVersionFileOnly = false
        signTag = false
    }
}

